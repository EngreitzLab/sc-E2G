from snakemake.utils import min_version
min_version("7.0")

import pandas as pd
import os
import yaml


configfile: "config/config.yaml"
conda: "mamba"

BIOSAMPLE_DF = pd.read_table(config["ABC_BIOSAMPLES"], na_values="").set_index("biosample", drop=False)

def make_paths_absolute(obj, base_path):
	"""
	Use absolute paths to be compatible with github submodules
	Recursively go through the dictionary and convert relative paths to absolute paths.
	"""
	if isinstance(obj, dict):
		for key, value in obj.items():
			obj[key] = make_paths_absolute(value, base_path)
	elif isinstance(obj, str):
		# We assume all strings are paths. If converting the string
		# to an absolute path results in a valid file, then the str was a path
		new_file = os.path.join(base_path, obj)
		if os.path.exists(new_file):
			return new_file
	return obj

config = make_paths_absolute(config, os.getcwd())
# Need to manually make results_dir an absolute path since above may
# not work if results_dir folder isn't created
# If results_dir is already an absolute path, this is a no-op
config["results_dir"] = os.path.join(os.getcwd(), config["results_dir"])
RESULTS_DIR = config["results_dir"]
E2G_DIR_PATH = config["E2G_DIR_PATH"]
MODEL_DIR = config["model_dir"]
include: os.path.join(E2G_DIR_PATH, "workflow", "rules", "utils.smk")

def get_e2g_config(config):
	e2g_config_file = os.path.join(E2G_DIR_PATH, "config/config.yaml")
	with open(e2g_config_file, 'r') as stream:
		e2g_config = yaml.safe_load(stream)
	e2g_config["E2G_DIR_PATH"] = E2G_DIR_PATH
	e2g_config["ABC_BIOSAMPLES"] = config["ABC_BIOSAMPLES"]
	e2g_config["results_dir"] = config["results_dir"]
	e2g_config["model_dir"] = config["model_dir"]
	config["MEGAMAP_HIC_FILE"] = e2g_config["MEGAMAP_HIC_FILE"]
	return e2g_config

module encode_e2g:
	snakefile:
		f"{E2G_DIR_PATH}/workflow/Snakefile"
	config: get_e2g_config(config)

use rule * from encode_e2g exclude add_external_features

rule add_external_features:
	input:
		predictions_extended = os.path.join(RESULTS_DIR, "{biosample}", "ActivityOnly_features.tsv.gz"),
		arc_predictions = os.path.join(RESULTS_DIR, "{biosample}", "EnhancerPredictionsAllPutative_ARC.tsv.gz"),
		feature_table_file = lambda wildcards: get_feature_table_file(wildcards.biosample)
	output:
		plus_external_features = os.path.join(RESULTS_DIR, "{biosample}", "ActivityOnly_plus_external_features.tsv.gz")
	conda:
		"envs/sc_e2g.yml"
	script:
		"scripts/integrate_kendall.R"

rule kendall_correlation:
	input:
		kendall_candidate_e2g_pairs = lambda wildcards: BIOSAMPLE_DF.loc[wildcards.biosample, 'candidate_pairs'],
		sc_atac_matix = lambda wildcards: BIOSAMPLE_DF.loc[wildcards.biosample, 'ATAC_matrix'],
		sc_rna_matix = lambda wildcards: BIOSAMPLE_DF.loc[wildcards.biosample, 'RNA_matrix']
	output:
		kendall_predictions = os.path.join(RESULTS_DIR, "{biosample}", "EnhancerPredictionsAllPutative_Kendall.tsv.gz") 
	conda:
		"envs/sc_e2g.yml"
	script:
		"scripts/compute_kendall.R"

rule arc_e2g:
	input:
		abc_predictions = os.path.join(RESULTS_DIR, "{biosample}", "Predictions", "EnhancerPredictionsAllPutative.tsv.gz"),
		kendall_predictions = os.path.join(RESULTS_DIR, "{biosample}", "EnhancerPredictionsAllPutative_Kendall.tsv.gz")
	output:
		arc_predictions = os.path.join(RESULTS_DIR, "{biosample}", "EnhancerPredictionsAllPutative_ARC.tsv.gz")
	conda:
		"envs/sc_e2g.yml"
	script:
		"scripts/compute_arc_e2g.R"

	